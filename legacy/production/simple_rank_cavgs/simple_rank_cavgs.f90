!==Program simple_rank_cavgs
!
! <rank_cavgs/begin> is a program for ranking class averages by decreasing population, given the
! stack of class averages (\texttt{stk} argument) and the 2D orientations document (\texttt{oritab})
! generated by \prgname{simple\_prime2D}. <rank_cavgs/end>
!
! The code is distributed with the hope that it will be useful, but _WITHOUT_ _ANY_ _WARRANTY_.
! Redistribution and modification is regulated by the GNU General Public License.
! *Author:* Hans Elmlund 2015
!
program simple_rank_cavgs
use simple_cmdline, only: cmdline
use simple_jiffys,  only: simple_end, nlines
use simple_params,  only: params
use simple_build,   only: build
use simple_oris,    only: oris
implicit none
type(params)         :: p
type(build)          :: b
type(cmdline)        :: cline
integer              :: iclass
integer, allocatable :: order(:)
if( command_argument_count() < 2 )then
    write(*,'(a)',advance='no') 'SIMPLE_RANK_CAVGS stk=<cavgs.ext> oritab=<2D clustering doc>'
    write(*,'(a)') ' [outstk=<ranked cavgs stack>]'
    stop
endif
call cline%parse
call cline%checkvar('stk',    1)
call cline%checkvar('oritab', 2)
call cline%check
call cline%set('prg', 'rank_cavgs')
p = params(cline)                   ! parameters generated
call b%build_general_tbox(p, cline) ! general objects built
p%ncls   = p%nptcls
p%nptcls = nlines(p%oritab) 
call b%a%new(p%nptcls)
call b%a%read(p%oritab)
order = b%a%order_cls()
do iclass=1,p%ncls
    write(*,'(a,1x,i5,1x,a,i5)') 'CLASS:', order(iclass), 'POP:', b%a%get_clspop(order(iclass)) 
    call b%img%read(p%stk, order(iclass))
    call b%img%write(p%outstk, iclass)
end do
call simple_end('**** SIMPLE_RANK_CAVGS NORMAL STOP ****')
end program simple_rank_cavgs